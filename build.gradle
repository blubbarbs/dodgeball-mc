plugins {
    id 'java'
    id("io.github.goooler.shadow") version "8.1.7"
//    id("io.papermc.paperweight.userdev") version "1.5.11" // Check for new versions at https://plugins.gradle.org/plugin/io.papermc.paperweight.userdev
}


group = 'com.gmail.blubberalls'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://hub.jeff-media.com/nexus/repository/jeff-media-public/'
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.3-R0.1-SNAPSHOT"
    implementation 'com.jeff_media:MorePersistentDataTypes:2.4.0'
}

shadowJar {
    relocate 'com.jeff_media.morepersistentdatatypes', 'com.gmail.blubberalls.morepersistentdatatypes'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

def server_path = "C:/Users/radar/Minecraft Server/plugins"
build {
//    dependsOn(reobfJar)
    dependsOn(shadowJar)
    doLast {
        copy {
            from "build/libs/Dodgeball-" + version + "-all.jar"
            into server_path
        }
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
